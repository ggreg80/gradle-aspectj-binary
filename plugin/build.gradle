group 'com.github.sedovalx.gradle'
def buildNumber = System.getenv("TRAVIS_BUILD_NUMBER")
version  buildNumber != null ? "1.0.$buildNumber" : '1.0-SNAPSHOT'

buildscript {
    ext {
        kotlin_version = '1.1.1'
        aspectjVersion = '1.8.10'
    }

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.aspectj:aspectjtools:$aspectjVersion"
    compile "commons-io:commons-io:2.5"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "1"
            name "Alexander Sedov"
            email "sedov.alx@gmail.com"
        }
    }
    
    scm {
       url "https://github.com/sedovalx/gradle-aspectj-binary"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifactId 'gradle-aspectj-binary'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Gradle AspectJ binary weaving plugin')
                root.appendNode('name', 'Gradle AspectJ binary weaving plugin')
                root.appendNode('url', 'https://github.com/sedovalx/gradle-aspectj-binary')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'com.github.sedovalx'
        name = 'com.github.sedovalx.gradle-aspectj-binary'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/sedovalx/gradle-aspectj-binary'
        publicDownloadNumbers = true
        version {
            name = "1.0.$buildNumber"
            desc = 'Gradle AspectJ binary weaving plugin'
            released = new Date()
            gpg {
                sign = true
            }
        }
    }

}
